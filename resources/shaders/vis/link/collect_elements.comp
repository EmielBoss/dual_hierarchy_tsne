/*
 * MIT License
 *
 * Copyright (c) 2021 Mark van de Ruit (Delft University of Technology)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

#version 460 core

#extension GL_KHR_shader_subgroup_basic : enable

struct Layout {
  uint offset;
  uint size;
};

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

// Buffer bindings
layout(binding = 0, std430) restrict readonly buffer Posi { int selectionBuffer[]; };
layout(binding = 1, std430) restrict readonly buffer Dsbl { int disabledBuffer[]; };
layout(binding = 2, std430) restrict readonly buffer Layo { Layout layoutsBuffer[]; };
layout(binding = 3, std430) restrict readonly buffer Neig { uint neighborsBuffer[]; };
layout(binding = 4, std430) restrict readonly buffer Sims { float similaritiesBuffer[]; };
layout(binding = 5, std430) restrict readonly buffer Scn { uint scanBuffer[]; };
layout(binding = 6, std430) restrict readonly buffer Min { float simMinBuffer[]; };
layout(binding = 7, std430) restrict readonly buffer Max { float simMaxBuffer[]; };
layout(binding = 8, std430) restrict writeonly buffer Idx { uint elementsBuffer[]; };
layout(binding = 9, std430) restrict writeonly buffer Rel { float simRelBuffer[]; };

// Uniform values
layout(location = 0) uniform uint nPoints;

// Shorthand subgroup/warp constants
const uint thread = gl_SubgroupInvocationID;
const uint nThreads = gl_SubgroupSize;

void main() {
  const uint i = (gl_WorkGroupSize.x * gl_WorkGroupID.x + gl_LocalInvocationID.x) / nThreads;

  if (i >= nPoints || selectionBuffer[i] == 0) { return; }
  
  Layout l = layoutsBuffer[i];
  float simRange = simMaxBuffer[i] - simMinBuffer[i];
  for (uint ij = l.offset + thread; ij < l.offset + l.size; ij += nThreads) {
    uint j = neighborsBuffer[ij];
    
    uint index = scanBuffer[i] + (ij - l.offset);
    elementsBuffer[index * 2] = i;
    elementsBuffer[index * 2 + 1] = disabledBuffer[j] == 1 ? i : j;
    simRelBuffer[index] = (similaritiesBuffer[ij] - simMinBuffer[i]) / simRange;
  }
}