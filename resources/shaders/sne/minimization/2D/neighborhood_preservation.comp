/*
 * MIT License
 *
 * Copyright (c) 2021 Mark van de Ruit (Delft University of Technology)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

#version 460 core

struct Layout {
  uint offset;
  uint size;
};

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

// Buffer bindings
layout(binding = 0, std430) restrict readonly buffer Layu { Layout layoutBuffer[]; };
layout(binding = 1, std430) restrict readonly buffer Neigh { uint neighborsBuffer[]; };
layout(binding = 2, std430) restrict buffer NeighEmb { uint neighborsEmbBuffer[]; };
layout(binding = 3, std430) restrict writeonly buffer NeighPres { float neighborhoodPreservationBuffer[]; };

// Uniform values
layout(location = 0) uniform uint nPoints;
layout(location = 1) uniform uint kParam;

void main() {
  const uint i = (gl_WorkGroupID.x * gl_WorkGroupSize.x + gl_LocalInvocationID.x);

  if (i >= nPoints) { return; }

  uint k = kParam - 1; // Actual value of k, since FAISS returns datapoint itself as nearest neighbor

  // Sort embedding neighbors buffer
  for (uint ijMax = i * kParam + k; ijMax > i * kParam + 1; ijMax--) { // Ignore the first "neighbour", as that's the datapoint itself
    for (uint ij = i * kParam + 1; ij < ijMax; ij++) {
      uint tmp = min(neighborsEmbBuffer[ij], neighborsEmbBuffer[ij+1]);
      neighborsEmbBuffer[ij+1] = neighborsEmbBuffer[ij] + neighborsEmbBuffer[ij+1] - tmp;
      neighborsEmbBuffer[ij] = tmp;
    }
  }

  Layout l = layoutBuffer[i];

  uint intersection = 0;
  uint uunion = 0; // union is a reserved keyword
  uint neighCounterHigh = 0;
  uint neighCounterEmbd = 0;
  while(neighCounterHigh < k && neighCounterEmbd < k) {
    uint neighIndexHigh = l.offset + neighCounterHigh;
    uint neighIndexEmbd = i * kParam + 1 + neighCounterEmbd;
    bool eq = neighborsBuffer[neighIndexHigh] == neighborsEmbBuffer[neighIndexEmbd];
    bool leq = neighborsBuffer[neighIndexHigh] < neighborsEmbBuffer[neighIndexEmbd];
    bool geq = neighborsBuffer[neighIndexHigh] > neighborsEmbBuffer[neighIndexEmbd];

    uunion++;
    intersection += int(eq);
    neighCounterHigh += int(eq || leq);
    neighCounterEmbd += int(eq || geq);
  }
  uunion += k - min(neighCounterHigh, neighCounterEmbd);
  neighborhoodPreservationBuffer[i] = float(intersection) / float(uunion);
}