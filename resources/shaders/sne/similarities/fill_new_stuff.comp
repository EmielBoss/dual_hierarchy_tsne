/*
 * MIT License
 *
 * Copyright (c) 2021 Mark van de Ruit (Delft University of Technology)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

#version 460 core

#extension GL_KHR_shader_subgroup_basic : enable

struct Layout {
  uint offset;
  uint size;
};

layout(local_size_x = 256, local_size_y = 1, local_size_z  = 1) in;

// Buffer bindings
layout(binding = 0, std430) restrict readonly buffer IdxSel { uint indicesBuffer[]; };
layout(binding = 1, std430) restrict readonly buffer LayPrv { Layout layoutsPrevBuffer[]; };
layout(binding = 2, std430) restrict readonly buffer Lay { Layout layoutsBuffer[]; };
layout(binding = 3, std430) restrict coherent buffer Cnt { uint countsBuffer[]; };
layout(binding = 4, std430) restrict buffer NeiA { uint neighborsAddedBuffer[]; };
layout(binding = 5, std430) restrict buffer Nei { uint neighborsBuffer[]; };
layout(binding = 6, std430) restrict writeonly buffer Sim { float similaritiesBuffer[]; };
layout(binding = 7, std430) restrict writeonly buffer SimO { float similaritiesBackup[]; };

// Uniform values
layout(location = 0) uniform float simAvg;
layout(location = 1) uniform uint nSelected;
layout(location = 2) uniform uint nSelectedOther;
layout(location = 3) uniform uint offsetOther;

const uint offset = offsetOther == 0 ? nSelectedOther : 0;

// Returns whether j is a neighbor of i
bool alreadyNeighbors(uint i, uint j, uint offset, uint size) {
  // Unfortunately the list is unsorted, so search time is linear
  for (uint ij = offset; ij < offset + size; ij++) {
    if (neighborsBuffer[ij] == j) {
      return true;
    }
  }
  return false;
}

// Calculates upper bound based on the order of magnitude of the selection size. Examples: upperBound(7) = 0.9,  upperBound(43) = 0.9, upperBound(123) = 0.99, upperBound(12368) = 0.9999
float upperBound(uint n) {
  float nDigits = floor(log2(float(n)) / log2(10)) + 1;
  float multiplier = pow(10.f, nDigits - 1);
  float bound = floor(0.999999999 * multiplier) / multiplier;
  return max(bound, 0.9);
}

float hash(uint a, uint b) {
  return fract(sin(dot(vec2(float(a), float(b)), vec2(12.9898,78.233))) * 43758.5453);
}

bool omitNeighbors(uint idx, uint size, uint jdx, uint sjze) {
  uint k_i = idx - offset;
  uint k_j = jdx - offsetOther;
  float omissionRatio = clamp(1.f - (size / 2.f) / nSelected, 0.f, upperBound(nSelected));
  float omjssionRatio = clamp(1.f - (sjze / 2.f) / nSelectedOther, 0.f, upperBound(nSelectedOther));
  float hashVal = hash(k_i + k_j, k_i * k_j);
  bool omit = hashVal < omissionRatio;
  bool omjt = hashVal < omjssionRatio;
  return omit || omjt;
}

const uint nSelectedMax = max(nSelected, nSelectedOther);

void main() {
  const uint idx = offset + (gl_WorkGroupID.x * gl_WorkGroupSize.x + gl_LocalInvocationID.x) / gl_SubgroupSize;

  if(idx - offset >= nSelected) { return; }

  const uint i = indicesBuffer[idx];
  Layout li = layoutsBuffer[i];
  Layout lip = layoutsPrevBuffer[i];

  for (uint jdx = offsetOther + gl_SubgroupInvocationID; jdx < offsetOther + nSelectedOther; jdx += gl_SubgroupSize) {
    uint j = indicesBuffer[jdx];
    Layout ljp = layoutsPrevBuffer[j];
    if(omitNeighbors(idx, lip.size, jdx, ljp.size) || alreadyNeighbors(i, j, li.offset, lip.size)) { continue; }
    uint k = atomicAdd(countsBuffer[idx], 1); // k is assigned the old value of countsBuffer[i]
    uint ij = li.offset + lip.size + k;
    neighborsAddedBuffer[idx * nSelectedMax + k] = j;
    neighborsBuffer[ij] = j;
    similaritiesBuffer[ij] = simAvg;
    similaritiesBackup[ij] = simAvg;
  }

}